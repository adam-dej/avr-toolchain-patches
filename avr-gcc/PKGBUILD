# $Id: PKGBUILD 122773 2014-11-19 17:36:29Z schuay $
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-gcc
_pkgname=gcc
pkgver=4.9.2
_islver=0.12.2
_cloogver=0.18.1
pkgrel=1
pkgdesc="The GNU AVR Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org/"
depends=('avr-binutils>=2.24' 'ppl' 'gcc-libs>=4.8.0' 'libmpc')
optdepends=('avr-libc: Standard C library for Atmel AVR development')
provides=("gcc-avr=$pkgver")
replaces=('gcc-avr')
options=('staticlibs' '!emptydirs' '!strip')
source=(http://ftp.gnu.org/gnu/gcc/${_pkgname}-${pkgver}/gcc-${pkgver}.tar.bz2
        http://isl.gforge.inria.fr/isl-${_islver}.tar.bz2
        http://www.bastoul.net/cloog/pages/download/cloog-${_cloogver}.tar.gz
        tiny441-tiny841.patch)

_basedir=${_pkgname}-${pkgver}

prepare() {
    cd ${srcdir}/gcc-${pkgver}

    # Add support for ATTiny441 and ATTiny841
    patch -p1 -i $srcdir/tiny441-tiny841.patch

    rm -rf ${srcdir}/${_builddir}
    mkdir ${srcdir}/${_builddir}
}

build() {
    # default CFLAGS lead to issues later on when configure 
    # calls avr-gcc with -march set.
    export CFLAGS="-O2 -pipe"
    export CXXFLAGS="-O2 -pipe"

    cd ${srcdir}/${_basedir} 

    # Link isl/cloog for in-tree builds
    ln -s ../isl-${_islver} isl
    ln -s ../cloog-${_cloogver} cloog

    # Do not install libiberty
    sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

    # https://bugs.archlinux.org/task/34629
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" gcc/configure

    echo ${pkgver} > gcc/BASE-VER

    cd ${srcdir}
    mkdir gcc-build && cd gcc-build

    # --disable-linker-build-id: https://bugs.archlinux.org/task/34902

    ${srcdir}/${_basedir}/configure \
                --disable-install-libiberty \
                --disable-libssp \
                --disable-libstdcxx-pch \
                --disable-libunwind-exceptions \
                --disable-linker-build-id \
                --disable-nls \
                --disable-werror \
                --enable-__cxa_atexit \
                --enable-checking=release \
                --enable-clocale=gnu \
                --enable-cloog-backend=isl \
                --enable-gnu-unique-object \
                --enable-gold \
                --enable-languages=c,c++ \
                --enable-ld=default \
                --enable-lto \
                --enable-plugin \
                --enable-shared \
                --infodir=/usr/share/info \
                --libdir=/usr/lib \
                --libexecdir=/usr/lib \
                --mandir=/usr/share/man \
                --prefix=/usr \
                --target=avr \
                --with-as=/usr/bin/avr-as \
                --with-gnu-as \
                --with-gnu-ld \
                --with-ld=/usr/bin/avr-ld \
                --with-plugin-ld=ld.gold \
                --with-system-zlib

    make
}

package() {
    cd ${srcdir}/gcc-build

    make -j1 DESTDIR=${pkgdir} install

    # Strip debug symbols from libraries; without this, the package size balloons to ~500MB.
    find ${pkgdir}/usr/lib -type f -name "*.a" \
        -exec /usr/bin/avr-strip --strip-debug '{}' \;

    # Install Runtime Library Exception
    install -Dm644 ${srcdir}/${_basedir}/COPYING.RUNTIME \
        ${pkgdir}/usr/share/licenses/${pkgname}/RUNTIME.LIBRARY.EXCEPTION

    rm -rf ${pkgdir}/usr/share/man/man7
    rm -rf ${pkgdir}/usr/share/info
}

md5sums=('4df8ee253b7f3863ad0b86359cd39c43'
         'e039bfcfb6c2ab039b8ee69bf883e824'
         'e34fca0540d840e5d0f6427e98c92252'
         'cecfec1550b35f026dcf28153179229a')
